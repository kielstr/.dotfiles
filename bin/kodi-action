#!/usr/bin/env perl

use 5.18.0;
use warnings;

my $script = $0;
$script =~ s!^.*/!!;

#http://kodi.wiki/view/list_of_built-in_functions
my %commands = (
    clean => {
        msg => 'Clean the Kodi library',
        cmd => 'CleanLibrary(video)',
    },

    msg => {
        msg => 'Write a message to the screen',
        cmd => 'Notification(%s, %s)',
        args => [qw( title message )],
    },

    play => {
        msg => 'Play a streaming video',
        cmd => 'PlayMedia(%s)',
        args => [qw( video-url )],
    },

    update => {
        msg => 'Update the Kodi library',
        cmd => 'UpdateLibrary(video)',
    },

    youtube => {
        msg => 'Play a youtube video',
        map_args => \&get_youtube_id,
        cmd => 'PlayMedia(%s)',
        args => [qw( youtube-url )],
    },
);

main();
exit 0;

sub main {
    usage() unless @ARGV;

    my $command = shift @ARGV;
    my @args = @ARGV;
    my $opts = $commands{$command} or usage("Unknown command \"$command\"");

    my $expected_args = @{ $opts->{args} // [] };
    my $got_args = @args;
    usage("Expected $expected_args args, but got $got_args")
        unless $got_args == $expected_args;

    if ($opts->{map_args}) {
        @args = $opts->{map_args}->(@args);
    }

    my $kodi_command = sprintf($opts->{cmd}, @args);
    exec('kodi-send', '--action', $kodi_command);
}

sub usage {
    say @_ if @_;
    say "usage: $script command [args...]";
    for my $command (sort keys %commands) {
        my $opts = $commands{$command};
        my $args = $opts->{args} // [ ];
        my $usage = join(' ', $command, @$args);
        printf("\t%-20s* %s\n", $usage, $opts->{msg});
    }
    exit 1;
}

sub get_youtube_id {
    my ($youtube_url) = @_;
    my $prefix = 'plugin://plugin.video.youtube/play/?video_id=';
    my $id = qr([a-zA-Z0-9_]+);

    if ($youtube_url =~ /watch\?v=($id)/) {
        return $prefix . $1;
    }
    if ($youtube_url =~ /youtu\.be\/($id)/) {
        return $prefix . $1;
    }

    die "Can't get video id from $youtube_url";
}
